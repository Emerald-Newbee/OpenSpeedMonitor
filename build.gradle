/*
* OpenSpeedMonitor (OSM)
* Copyright 2014 iteratec GmbH
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*   http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/
buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath 'org.grails.plugins:database-migration:2.0.0.RC4'
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.11.2"
        classpath 'com.bertramlabs.plugins:less-asset-pipeline:2.11.2'
        classpath "org.grails.plugins:hibernate4:5.0.4"
        classpath 'com.moowork.gradle:gradle-node-plugin:1.0.1'
    }
}

version "4.1.0_beta"
group "OpenSpeedMonitor"

apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "war"
apply plugin: "org.grails.grails-web"
apply plugin: "org.grails.grails-gsp"
apply plugin: "asset-pipeline"
apply plugin: 'com.moowork.node'

ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion

    karmaExec = file('node_modules/karma/bin/karma')
    karmaConfig = 'src/test/js/karma.conf.js'

    seleniumVersion = "2.53.1"
    phantomJsVersion = '2.1.1'
}

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "http://dl.bintray.com/iteratec/grails-plugins" }
    maven { url "http://dl.bintray.com/ishults/plugins" }
    maven { url "https://dl.bintray.com/longwa/plugins" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    // Default Grails-Stuff
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    provided "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-dependencies"
    compile "org.grails:grails-web-boot"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:scaffolding"
    compile "org.grails.plugins:hibernate4"
    compile 'org.grails.plugins:log-hibernate-stats:1.0.20'
    compile "org.hibernate:hibernate-ehcache"
    runtime "com.h2database:h2"
    console "org.grails:grails-console"
    runtime 'org.grails.plugins:grails-console:2.0.4'
    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails.plugins:geb"
    testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
    testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"
    profile "org.grails.profiles:web:3.1.5"

    testCompile "org.grails.plugins:hibernate"
    testCompile "org.seleniumhq.selenium:selenium-support:${seleniumVersion}"
    testCompile "org.seleniumhq.selenium:selenium-remote-driver:${seleniumVersion}"
    testCompile "org.seleniumhq.selenium:selenium-firefox-driver:${seleniumVersion}"
    testCompile "org.seleniumhq.selenium:selenium-chrome-driver:${seleniumVersion}"

    //Custom OSM-Stuff
    runtime 'mysql:mysql-connector-java:5.1.26'
    compile 'com.bertramlabs.plugins:less-asset-pipeline:2.9.4'
    runtime 'org.grails.plugins:database-migration:2.0.0.RC4'
    compile 'net.sf.supercsv:super-csv:2.4.0'
    compile 'org.grails.plugins:joda-time:2.0.0'
    compile 'org.grails.plugins:spring-security-core:3.1.1'
    compile 'org.grails.plugins:spring-security-ui:3.0.0.M2'
    compile 'org.grails.plugins:greenmail:2.0.0.M1'
    compile 'org.grails.plugins:mail:2.0.0.RC6'
    compile 'org.grails.plugins:quartz:2.0.8'
    compile 'org.grails.plugins:http-builder-helper:1.0.2'
    compile 'org.grails.plugins:taggable:2.0.1-SNAPSHOT'
    compile 'org.grails.plugins:cookie:2.0.4'
    compile 'xml-apis:xml-apis:1.4.01'                          //new dependency
    compile 'io.dropwizard.metrics:metrics-core:3.1.0'
    compile 'io.dropwizard.metrics:metrics-graphite:3.1.2'
    compile 'io.dropwizard.metrics:metrics-jvm:3.1.2'
    compile 'p6spy:p6spy:3.0.0'


    runtime 'net.sf.supercsv:super-csv:2.4.0'
    runtime "org.grails.plugins:asset-pipeline"
    assets 'com.bertramlabs.plugins:less-asset-pipeline:2.9.4'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'software.betamax:betamax-junit:2.0.0-alpha-1'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile('com.codeborne:phantomjsdriver:1.2.1') {
        transitive = false
    }

}
task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

assets {
    minifyJs = true
    minifyCss = true
    configOptions = [:]
    includes = ['bootstrap-custom.less', 'job/list.less']
    excludes = ['**/*.less']
}

node {
    //node version to use
    version = '6.2.2'
    //npm version to use
    npmVersion = '3.10.3'
    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true
    // Set the work directory for unpacking node
    workDir = file("${project.buildDir}/nodejs")
    // Set the work directory where node_modules should be located
//    nodeModulesDir = file("${project.projectDir}/node_modules")
    nodeModulesDir = file("${project.projectDir}")
}


npmInstall {
    outputs.files file('node_modules')
}

task jasmineRun(type: NodeTask, dependsOn: ['bowerPackages']) {
    script = karmaExec
    args = ['start', karmaConfig, '--single-run']
}

task jasmineWatch(type: NodeTask, dependsOn: 'npmInstall') {
    script = karmaExec
    args = ['start', karmaConfig]
}

test.dependsOn jasmineRun


task bowerInstall(type: NodeTask) {
    script = file('node_modules/bower/bin/bower')
    args = ["--config.storage.cache=${gradle.getGradleUserHomeDir()}/caches/bower/cache",
            "--config.storage.packages=${gradle.getGradleUserHomeDir()}/caches/bower/packages",
            "--config.storage.registry=${gradle.getGradleUserHomeDir()}/caches/bower/registry",
            'install']
    inputs.files file('bower.json')
    outputs.files file('bower_components')
    dependsOn npmInstall
}

task bowerSyncJavascript(type: Sync) {
    from 'bower_components'
    into "grails-app/assets/javascripts/bower_components"
    include 'jquery/jquery.min.js'
    include 'jquery-ui/ui/minified/jquery-ui.min.js'
    include 'bootstrap/dist/js/bootstrap.min.js'
    include 'clipboard/dist/clipboard.min.js'
    include 'jQuery-contextMenu/dist/jquery.contextMenu.min.js'
    include 'chosen/chosen.jquery.js'
    include 'd3/d3.min.js'
    include 'bootstrap-datepicker/dist/js/bootstrap-datepicker.min.js'
    include 'later/later.min.js'
    include 'moment/min/moment.min.js'
    include 'prettycron/prettycron.js'
    include 'spin.js/spin.js'
    include 'tagit/js/tag-it.min.js'
    include 'codemirror/lib/codemirror.js'
    include 'codemirror/addon/hint/show-hint.js'
    include 'bootstrap-timepicker/js/bootstrap-timepicker.js'
    include 'dimple/dist/dimple.latest.min.js'
    include 'StickyTableHeaders/js/jquery.stickytableheaders.js'
    include 'file-saver/FileSaver.min.js'
    dependsOn bowerInstall
}

task bowerSyncStylesheets(type: Sync) {
    from 'bower_components'
    into "grails-app/assets/stylesheets/bower_components"
    include 'bootstrap/dist/css/bootstrap*.min.css'
    include 'bootstrap/less/*.less'
    include 'bootstrap/less/mixins/*.less'
    include 'components-font-awesome/css/font-awesome.min.css'
    include 'components-font-awesome/fonts/*'
    include 'jQuery-contextMenu/dist/jquery.contextMenu.min.css'
    include 'chosen/chosen.css'
    include 'chosen/chosen-sprite.png'
    include 'chosen/chosen-sprite@2x.png'
    include 'bootstrap-datepicker/dist/css/bootstrap-datepicker.min.css'
    include 'codemirror/lib/codemirror.css'
    include 'codemirror/addon/hint/show-hint.css'
    include 'tagit/css/jquery.tagit.css'
    include 'jquery-ui/themes/smoothness/jquery-ui.min.css'
    include 'jquery-ui/themes/smoothness/images/'
    include 'bootstrap-timepicker/css/timepicker.less'
    dependsOn bowerInstall
}


task bowerPackages() {
    dependsOn bowerSyncJavascript, bowerSyncStylesheets
}

processResources.dependsOn bowerPackages
assetCompile.dependsOn bowerPackages

clean.delete << file('grails-app/assets/javascripts/bower_components')
clean.delete << file('grails-app/assets/stylesheets/bower_components')
clean.delete << file('node_modules')
clean.delete << file('bower_components')



sourceSets {
    main {
        resources {
            srcDir 'grails-app/migrations'
        }
    }
}
'dbmGormDiff' {
    jvmArgs = ["-XX:MaxPermSize=512m", "-Xms1G", "-Xmx2G"]
}

'dbmUpdate' {
    jvmArgs = ["-XX:MaxPermSize=512m", "-Xms1G", "-Xmx2G"]
}
'dbmClearChecksums' {
    jvmArgs = ["-XX:MaxPermSize=512m", "-Xms1G", "-Xmx2G"]
}
'dbmChangelogSync' {
    jvmArgs = ["-XX:MaxPermSize=512m", "-Xms1G", "-Xmx2G"]
}